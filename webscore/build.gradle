buildscript {
    ext.kotlin_version = '1.2.50'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}


apply plugin: 'kotlin2js'

compileKotlin2Js {
    // TODO Just here to check whether this has an effect on getting things to work in a webview on Android
    kotlinOptions {
        sourceMap = true
        sourceMapEmbedSources = "always"
    }
}


repositories {
    mavenCentral()
}

dependencies {
    compile project(':score-js')
    compile libraries.kotlin_stdlib_js
    compile "org.jetbrains.kotlinx:kotlinx-html-js:0.6.10"
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
}


task assembleWeb(type: Sync) {
    configurations.compile.each { File file ->
        from(zipTree(file.absolutePath), {
            includeEmptyDirs = false
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") ||
                        !path.startsWith("META-INF/"))
            }
        })
    }
    from compileKotlin2Js.destinationDir
    into "${projectDir}/web"

    dependsOn classes
}

assemble.dependsOn assembleWeb
